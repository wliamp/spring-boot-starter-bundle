plugins {
    id 'org.jetbrains.kotlin.jvm'
    id 'org.jetbrains.kotlin.plugin.spring'
    id 'org.springframework.boot'
    id 'io.spring.dependency-management'
    id 'maven-publish'
    id 'signing'
    id 'jacoco'
}

dependencies {
    implementation 'org.springframework.security:spring-security-oauth2-jose'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.jetbrains.kotlin:kotlin-test-junit5'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'spring-boot-starter-reactive-token'
            pom {
                name.set('Spring Boot Starter Reactive Token')
                description.set('Custom Spring Boot starter reactive for token utilities.')
            }
        }
    }
}

tasks.named('test') {
    useJUnitPlatform()
    finalizedBy(tasks.jacocoTestReport)
}

tasks.named('jacocoTestReport', JacocoReport) {
    dependsOn tasks.test
    reports {
        xml.required.set(true)
        html.required.set(true)
    }
}
